#!/usr/bin/env perl
#
# InspIRCd -- Internet Relay Chat Daemon
#
#   Copyright (C) 2013-2016, 2018-2020 Sadie Powell <sadie@witchery.services>
#
# This file is part of InspIRCd.  InspIRCd is free software: you can
# redistribute it and/or modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation, version 2.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#


BEGIN {
	require 5.10.0;
	unless (-f 'configure') {
		print "Error: $0 must be run from the main source directory!\n";
		exit 1;
	}
}

use feature ':5.10';
use strict;
use warnings FATAL => qw(all);

use File::Basename qw(dirname);
use FindBin        qw($RealDir);

use lib dirname $RealDir;
use make::common;
use make::configure;

$ENV{INSPIRCD_DEBUG} = 3;
$ENV{INSPIRCD_VERBOSE} = 1;

execute 'git', 'clean', '-dfx';

my @compilers = $#ARGV >= 0 ? @ARGV : qw(g++ clang++ icpc);
foreach my $compiler (@compilers) {
	if (system "$compiler -v > /dev/null 2>&1") {
		say "Skipping $compiler as it is not installed on this system!";
		next;
	}
	$ENV{CXX} = $compiler;
	my @socketengines = qw(select);
	push @socketengines, 'epoll' if test_header $compiler, 'sys/epoll.h';
	push @socketengines, 'kqueue' if test_file $compiler, 'kqueue.cpp';
	push @socketengines, 'poll' if test_header $compiler, 'poll.h';
	foreach my $socketengine (@socketengines) {
		say "Attempting to build using the $compiler compiler and the $socketengine socket engine...";
		my @configure_flags;
		if (defined $ENV{TEST_BUILD_MODULES}) {
			execute './configure', '--enable-extras', $ENV{TEST_BUILD_MODULES};
			push @configure_flags, '--disable-auto-extras';
		}
		if (execute './configure', '--development', '--socketengine', $socketengine, @configure_flags) {
			say "Failed to configure using the $compiler compiler and the $socketengine socket engine!";
			exit 1;
		}
		if (execute 'make', '--jobs', get_cpu_count, 'install') {
			say "Failed to compile using the $compiler compiler and the $socketengine socket engine!";
			exit 1;
		}
		say "Building using the $compiler compiler and the $socketengine socket engine succeeded!";
	}

	execute 'git', 'clean', '-dfx';
}

#!/usr/bin/env perl
#
# InspIRCd -- Internet Relay Chat Daemon
#
#   Copyright (C) 2013-2016, 2019-2022 Sadie Powell <sadie@witchery.services>
#
# This file is part of InspIRCd.  InspIRCd is free software: you can
# redistribute it and/or modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation, version 2.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#


use v5.10.0;
use strict;
use warnings FATAL => qw(all);

use File::Basename qw(dirname);
use FindBin        qw($RealDir);

use lib dirname $RealDir;
use make::common;
use make::configure;

$ENV{INSPIRCD_DEBUG} = 3;
$ENV{INSPIRCD_VERBOSE} = 1;

execute 'git', 'clean', '-dfx';

my $root = dirname $RealDir;
my @compilers = $#ARGV >= 0 ? @ARGV : qw(g++ clang++ icpx icpc);
for my $compiler (@compilers) {
	if (system "$compiler -v > /dev/null 2>&1") {
		say STDERR "Skipping $compiler as it is not installed on this system!";
		next;
	}
	$ENV{CXX} = $compiler;
	my @socketengines = qw(select);
	push @socketengines, 'epoll' if test_header $compiler, 'sys/epoll.h';
	push @socketengines, 'kqueue' if test_file $compiler, 'kqueue.cpp';
	push @socketengines, 'poll' if test_header $compiler, 'poll.h';
	for my $socketengine (@socketengines) {
		say "Attempting to build using the $compiler compiler and the $socketengine socket engine...";
		my @configure_flags;
		push @configure_flags, '--disable-ownership' unless $>;
		if (defined $ENV{TEST_BUILD_MODULES}) {
			execute "$root/configure", '--enable-extras', $ENV{TEST_BUILD_MODULES};
			push @configure_flags, '--disable-auto-extras';
		}
		if (execute "$root/configure", '--development', '--socketengine', $socketengine, @configure_flags) {
			say STDERR "Failed to configure using the $compiler compiler and the $socketengine socket engine!";
			exit 1;
		}
		if (execute 'make', '--directory', $root, '--jobs', get_cpu_count() + 1, 'install') {
			say STDERR "Failed to compile using the $compiler compiler and the $socketengine socket engine!";
			exit 1;
		}
		say "Building using the $compiler compiler and the $socketengine socket engine succeeded!";
	}

	execute 'git', 'clean', '-dfx';
}

#!/usr/bin/env perl
#
# InspIRCd -- Internet Relay Chat Daemon
#
#   Copyright (C) 2021 Sadie Powell <sadie@witchery.services>
#
# This file is part of InspIRCd.  InspIRCd is free software: you can
# redistribute it and/or modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation, version 2.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
# details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#


use v5.10.0;
use strict;
use warnings FATAL => qw(all);

use File::Basename        qw(dirname);
use File::Spec::Functions qw(catfile);
use FindBin               qw($RealDir);

use lib dirname $RealDir;
use make::common;
use make::console;

my %authors;
for my $author (split /\n+/, `git log --pretty='%aN <%aE>' HEAD`) {
	$author = $1 if $author =~ /^(.+) <(?:unknown\@email.invalid|\S+\@users.noreply.github.com)>$/;

	$authors{$author} ||= 0;
	$authors{$author} += 1;
}

my $author_file = catfile dirname($RealDir), 'docs', 'AUTHORS.txt';
open(my $fh, '>', $author_file) or print_error "unable to write $author_file: $!";
say $fh <<"EOH";
Since the first commit in January 2003 ${\scalar %authors} people have submitted patches,
commits, and other useful contributions to InspIRCd. These people, ordered by
the number of contributions they have made, are:

EOH

for my $author (sort { $authors{$b} <=> $authors{$a} or $a cmp $b } keys %authors) {
	next if $author eq 'InspIRCd Robot <noreply@inspircd.org>';
	say $fh "  * $author";
}
close $fh;

execute 'git', 'commit',
	'--author', 'InspIRCd Robot <noreply@inspircd.org>',
	'--message', 'Update author list.',
	'--', $author_file;
